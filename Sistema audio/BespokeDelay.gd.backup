extends ComponenteAudio
class_name BespokeDelay

var buffer : Array[float]
var buffer_pointer := 0

@export var campionatore : CampionatorePistone
@export_range(4,4000) var buffer_len := 5
@export var feedback := 0.8
@export var invert := false
@export_range(0.01,10) var delay_length_multiplier := 1.0
@export_range(0.01,2.0) var gain := 1.0

@onready var samp_rate_ratio = 44100 / InfoAudio.frequenza_campionamento_hz


func _enter_tree():
	buffer.resize(buffer_len)


func _physics_process(delta):
	_update_params()


func _update_params():
	samp_rate_ratio = 44100 / InfoAudio.frequenza_campionamento_hz


func ottieni_campione() -> float:
	buffer_pointer += 1
	
	var delay_samps : float =\
		campionatore.ottieni_riverbero() * delay_length_multiplier * samp_rate_ratio
	delay_samps = clampf(delay_samps, 0.01, buffer_len-2)

	var sample : float = read_buffer(-roundi(delay_samps) - 1)
	var next_sample : float = read_buffer(-roundi(delay_samps))
	var delayed_sample :=\
		lerpf(sample, next_sample, delay_samps - roundi(delay_samps))

	write_buffer(0,campionatore.ottieni_campione())
	
	var delay_input = delayed_sample * feedback * (-1 if invert else 1)
	
	add_to_buffer(0, delay_input)

	var out := read_buffer(0) * gain
	
	return out


func read_buffer(offset : int) -> float:
	var i := posmod(buffer_pointer + offset, buffer_len)
	return buffer[i]

func write_buffer(offset : int, input : float):
	var i := posmod(buffer_pointer + offset, buffer_len)
	buffer[i] = input

func add_to_buffer(offset : int, input : float):
	var i := posmod(buffer_pointer + offset, buffer_len)
	buffer[i] += input
